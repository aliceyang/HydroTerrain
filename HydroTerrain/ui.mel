// Alice Yang | Cheng Xie
// CIS 660 Authoring Tool
// This is the GUI code for the HydroTerrain maya plug-in

// git status -uno (--untracked-files=no)
// git commit -m "commit message" -a // commit with auto staging
// git push origin master // Push commits to remote repo
// git log origin/master..HEAD // To show commits that have not been pushed to remote repo


//------------------------------------------------------------
// Global variables to keep track of LSystemCmd input values
//------------------------------------------------------------
global string $grammarPath;
global string $grammar;
global string $grammarScrollField;
global string $iterationSlider;
global string $stepSizeSlider;
global string $angleSlider;
global int $developing = true; // Set to false when ready to release

//------------------------------------------------------------
// Creates the main window LSystem menu
//------------------------------------------------------------
global proc createLSystemUI()
{
    global int $developing;
    if ($developing && `menu -exists LSystemMenu`)
        deleteUI LSystemMenu;
    if (!`menu -exists LSystemMenu`)
    {
        global string $gMainWindow;
        setParent $gMainWindow;
        menu -label "LSystem" LSystemMenu;
            menuItem -label "Call LSystem Command" -command displayLSystemDialog;
            menuItem -divider true;
            menuItem -label "Create LSystem Node" -command createLSystemNode;      
    }
}

//------------------------------------------------------------
// Creates an LSystem Node
//------------------------------------------------------------
global proc createLSystemNode()
{
    createNode transform -n LSystem1;
    createNode mesh -n LSystemShape1 -p LSystem1;
    sets -add initialShadingGroup LSystemShape1;
    createNode LSystemNode -n LSystemNode1;
    connectAttr time1.outTime LSystemNode1.time;
    connectAttr LSystemNode1.outputMesh LSystemShape1.inMesh;
}

//------------------------------------------------------------
// Displays the popup GUI dialog
//------------------------------------------------------------
global proc displayLSystemDialog()
{
    global int $developing;
    global string $grammarScrollField;
    
    if ($developing && `window -exists myWindow`)
        deleteUI myWindow;
    if (!`window -exists myWindow`)
        window -widthHeight 500 300 -title "Call LSystem Command" myWindow;
    
    columnLayout -adjustableColumn true;          
        string $formBrowse = `formLayout -numberOfDivisions 100`;
            string $bBrowse = `button -command openFileDialog -label "Browse..."`;
            string $tBrowse = `text -font boldLabelFont -label "Browse for grammar files"`;
            formLayout -edit
                -attachForm $tBrowse "left" 0
                -attachForm $bBrowse "right" 0
                $formBrowse;
            setParent ..;
        $grammarScrollField = `scrollField -wordWrap off -height 150 -text "Grammar displayed here..."`;
        setParent ..;
        
    global string $iterationSlider;
    global string $stepSizeSlider;
    global string $angleSlider;
    
    frameLayout -label "LSystem Parameters";
    $iterationSlider = `intSliderGrp -label "Iterations" -field true`;
    $stepSizeSlider = `floatSliderGrp -label "Default step size" -field true`;
    $angleSlider = `floatSliderGrp -label "Default angle" -field true`;
    setParent ..;
    
    string $formConfirm = `formLayout -numberOfDivisions 100`;
    string $bCreate = `button -label "Create" -command callLSystemCmd`;
    string $bCancel = `button -label "Cancel" -command deleteLSystemUI`;
    formLayout -edit
        -attachForm $bCreate "left" 0
        -attachForm $bCancel "right" 0
        $formConfirm;
    
    showWindow myWindow;
}

//-------------------------------------------------------------
// Calls LSystemCmd with argument values taken from the sliders
//-------------------------------------------------------------
global proc callLSystemCmd()
{
    global string $iterationSlider;
    global string $stepSizeSlider;
    global string $angleSlider;
    global string $grammar;
    
    int $i = `intSliderGrp -q -v $iterationSlider`;
    float $ss = `floatSliderGrp -q -v $stepSizeSlider`;
    float $a = `floatSliderGrp -q -v $angleSlider`;
    
    print ("INFO: " + "iteration = " + $i + " stepSize = " + $ss + " angle = " + $a + " grammar = " + $grammar + "\n");
    LSystemCmd -ss $ss -i $i -a $a -g $grammar;
}

//--------------------------------------------------------------
// Sets the grammar file path and displays the text file
//--------------------------------------------------------------
global proc openFileDialog()
{
    global string $grammarPath;
    global string $grammar;
    global string $grammarScrollField;
    
    $grammarPath = `fileDialog -directoryMask "*.txt"`;
    print ("INFO: $grammarPath = " + $grammarPath + "\n");
    
    $file = `fopen $grammarPath "r"`;
    $grammar = `fread $file $grammar`;
    scrollField -edit -text $grammar $grammarScrollField;
    fclose $file;
}

//------------------------------------------------------------
// Deletes the GUI dialog
//------------------------------------------------------------
global proc deleteLSystemUI()
{
    if (`window -exists myWindow`)
        deleteUI myWindow;
}
